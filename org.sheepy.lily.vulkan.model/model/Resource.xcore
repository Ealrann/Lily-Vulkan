@GenModel(cleanup="true", codeFormatting="true")
@GenModel(modelDirectory="/org.sheepy.lily.vulkan.model/src/generated/java")
@GenModel(editDirectory="/org.sheepy.lily.vulkan.model.edit/src/generated/java")
@GenModel(publicConstructors="true", complianceLevel="11.0", resource="XMI")
@GenModel(extensibleProviderFactory="true", childCreationExtenders="true")
@GenModel(updateClasspath="false")
package org.sheepy.lily.vulkan.model.resource

import java.nio.ByteBuffer
import org.sheepy.lily.core.model.types.LNamedElement
import org.sheepy.lily.vulkan.model.IResource
import org.sheepy.vulkan.model.enumeration.EAccess
import org.sheepy.vulkan.model.enumeration.EBorderColor
import org.sheepy.vulkan.model.enumeration.EBufferUsage
import org.sheepy.vulkan.model.enumeration.EDescriptorType
import org.sheepy.vulkan.model.enumeration.EFilter
import org.sheepy.vulkan.model.enumeration.EFormat
import org.sheepy.vulkan.model.enumeration.EImageLayout
import org.sheepy.vulkan.model.enumeration.EImageUsage
import org.sheepy.vulkan.model.enumeration.EPipelineStage
import org.sheepy.vulkan.model.enumeration.ESamplerAddressMode
import org.sheepy.vulkan.model.enumeration.ESamplerMipmapMode
import org.sheepy.vulkan.model.enumeration.EShaderStage
import org.sheepy.vulkan.model.barrier.AbstractImageBarrier
import org.sheepy.vulkan.model.barrier.AbstractBufferBarrier

class BasicResource extends IResource
{
}

class PushBuffer extends IResource
{
	long size
	int instanceCount = "3"
}

interface DescriptedResource extends IResource
{
}

abstract class BasicDescriptedResource extends DescriptedResource
{
	contains Descriptor descriptor
}

class Buffer extends BasicDescriptedResource
{
	long size
	unique EBufferUsage[] usages
	ByteBuffer data
	boolean oftenUpdated = "false"
	boolean gpuBuffer = "true"
	@GenModel(propertyDescription="Every push in this buffer will write on the next instance, and update the descriptor acordingly")
	int instanceCount = "1"
}

class CompositeBuffer extends DescriptedResource
{
	contains BufferDataProvider[] dataProviders
}

class BufferDataProvider extends Descriptor, LNamedElement
{
	long size
	EBufferUsage usage
	int instanceCount = "1"
}

class DescribedDataProvider extends BufferDataProvider, Descriptor
{
}

type ByteBuffer wraps ByteBuffer

class Image extends BasicDescriptedResource
{
	int width
	int height
	EFormat format
	unique EImageUsage[] usages
	int properties
	int tiling = "0"
	int mipLevels = "1"
	contains ImageLayout initialLayout
	boolean fillWithZero = "false"
}

class ImageLayout
{
	EPipelineStage stage
	EImageLayout layout
	unique EAccess[] accessMask
}

class SampledImage extends BasicDescriptedResource
{
	contains Sampler sampler
}

class Semaphore extends BasicResource, LNamedElement
{
	boolean signalizedAtInit = "false"
	EPipelineStage waitStage
}

class Font extends SampledImage
{
	contains PathResource file
	int height = "18"
}

abstract class AbstractTexture extends SampledImage
{
	boolean mipmapEnabled = "false"
}

class Texture extends AbstractTexture
{
	contains PathResource file
}

class Sampler
{
	EFilter minFilter = "NEAREST"
	EFilter magFilter = "NEAREST"
	ESamplerMipmapMode mipmapMode = "NEAREST"
	ESamplerAddressMode addressMode = "REPEAT"
	EBorderColor borderColor = "INT_OPAQUE_BLACK"
	boolean anisotropyEnabled = "false"
	boolean unnormalizedCoordinates = "false"
	boolean compareEnable = "false"
	float lodBias = "0"
	int minLod = "0"
	int maxLod = "1"
	float maxAnisotropy = "1"
}

type JavaModule wraps Module

class Descriptor
{
	EDescriptorType descriptorType
	unique EShaderStage[] shaderStages
}

class DescriptorSet extends LNamedElement
{
	refers DescriptedResource[1..*] descriptors
}

class DescriptorSetPkg
{
	contains DescriptorSet[] descriptorSets
}

class BufferBarrier extends AbstractBufferBarrier
{
	refers Buffer buffer
}

class ImageBarrier extends AbstractImageBarrier
{
	refers Image image
}

// ---------------------
// Non pipeline resource: Vulkan doesn't need a descriptor for them
class Shader extends BasicResource
{
	contains PathResource file
	EShaderStage stage
}

abstract class PathResource extends BasicResource
{
	String path
}

class FileResource extends PathResource
{
}

abstract class AbstractModuleResource extends PathResource
{
}

class ModuleResource extends AbstractModuleResource
{
	JavaModule module
}

class StringModuleResource extends AbstractModuleResource
{
	String moduleName
}
