import org.gradle.internal.os.OperatingSystem

switch (targetPlatform) {
	case OperatingSystem.LINUX:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
				? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
				: "natives-linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
}

dependencies {
	api project(':org.sheepy.lily.vulkan.extra.api')
	implementation project(':org.sheepy.lily.vulkan.core')

	api "org.lwjgl:lwjgl-stb:$lwjglVersion"
	api "org.lwjgl:lwjgl-nuklear:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-stb:$lwjglVersion:$lwjglNatives"
	implementation "org.lwjgl:lwjgl-nuklear:$lwjglVersion:$lwjglNatives"

	// Test
	testRuntimeOnly 'org.sheepy.lily.core:org.sheepy.lily.core.base'

	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
	testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"

	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
}

test {
	useJUnitPlatform()
}

sourceSets {
	main {
		java.srcDirs = ['src/main/java',
						'src/main/shader']
	}
}
